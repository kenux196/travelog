buildscript {
    dependencies {
        classpath 'org.flywaydb:flyway-mysql:9.21.1'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id "org.flywaydb.flyway" version "10.9.1"
    id 'jacoco'
}

group = 'me.kenux'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // Querydsl for spring boot 3.0
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // flyway db migration
//    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // jwt : https://github.com/jwtk/jjwt
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.12.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.12.5'

    // 설정파일 암호화
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.5'

    // time based uuid generator
    implementation 'com.fasterxml.uuid:java-uuid-generator:4.3.0'

    // java faker
    testImplementation 'net.datafaker:datafaker:2.1.0'

    // swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0' // spring boot 3.0 이상에서 사용
//    implementation 'org.springdoc:springdoc-openapi-ui:1.7.0' // spring boot 3.0 이하에서 사용
}

//Querydsl 추가, 자동 생성된 Q클래스 gradle clean으로 제거
clean {
    delete file('src/main/generated')
}

ext.profile = (!project.hasProperty('profile') || !profile) ? 'local' : profile

flyway {
    configFiles = ["${file('src/main/resources/db/config/flyway-' + profile + '.conf').absolutePath}"]
    locations = ["filesystem:${file('src/main/resources/db/sql').absolutePath}"]
    encoding = 'UTF-8'
    outOfOrder = true // 여러브랜치에서 서로 다른 날짜로 만들어도 작동하도록
    validateOnMigrate = true
    // locations = ["classpath:sql"] gradle CLI 를 사용하는 경우 classpath 는 인식 하지 못함.
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy(tasks.jacocoTestReport)
}

jacoco {
    toolVersion = '0.8.10'
}

tasks.jacocoTestReport {
    dependsOn(tasks.test)

    reports {
        html.enabled true
        xml.enabled true
        csv.enabled false
    }

    def Qdomains = []
    for (qPattern in '**/QA'..'**/QZ') {
        Qdomains.add(qPattern + '*')
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/config/*",
                    "**/exception/*",
                    "**/jwt/*",
                    "**/dto/*",
                    "**/*Application*",
                    "**/*Request*",
                    "**/*Response*",
                    "**/*Dto*",
                    "**/test/*",
                    "**/domain/common/*"
//                    "**/*Filter*",
//                    "**/*Exception*"
            ] + Qdomains)
        }))
    }

    finalizedBy(tasks.jacocoTestCoverageVerification)
}

tasks.jacocoTestCoverageVerification {
    dependsOn(tasks.jacocoTestReport)

    def Qdomains = []
    for (qPattern in '*.QA'..'*.QZ') {
        Qdomains.add(qPattern + '*')
    }

    violationRules {
        rule {
            enabled = true
            element = 'CLASS'
            // includes = []

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.50
            }

            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }

            excludes = ["**.config.*",
                        "**.jwt.*",
                        "**.exception.*",
                        "**.dto.*",
                        "**.*Application*",
                        "**.*Request*",
                        "**.*Response*",
                        "**.*Dto*",
                        "**.test.*",
                        "**.domain.common.*"
//                        "**/*Filter*",
//                        "**/*Exception*"
            ] + Qdomains
        }
    }
}
